# Copyright 2025 EngFlow, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

"""rules_magic module configuration"""

module(name = "rules_magic")

bazel_dep(name = "rules_java", version = "7.6.0")
single_version_override(
    module_name = "rules_java",
    version = "9.0.3",
)

# Importing `protobuf` without `repo_name = "com_google_protobuf"` breaks
# compatibility with legacy `WORKSPACE` builds. Bazel will silently use a
# different `com_google_protobuf` version for its `@bazel_tools` repo. Bazel 6
# and 7 builds will complete, but Bazel 8 builds will fail.
bazel_dep(
    name = "protobuf",
    version = "28.0",
    repo_name = "com_google_protobuf",
)
single_version_override(
    module_name = "protobuf",
    version = "33.0",
)

# Note that the shared Bazel worker proto definition from `@bazel_worker_api` is
# a completely internal detail that shouldn't really affect users. Newer
# protocol buffer versions are generally compatible with older versions. For
# that reason, this `bazel_dep` doesn't have a corresponding
# `single_version_override`.
bazel_dep(name = "bazel_worker_api", version = "0.0.6")

config = use_extension(
    "//magic/extensions:config.bzl",
    "magic_config",
)
use_repo(config, "rules_magic_config")

dev_config = use_extension(
    "//magic/extensions:config.bzl",
    "magic_config",
    dev_dependency = True,
)
dev_config.settings(
    versions = [
        "1.2.3",
        "4.5.6",
        "7.8.9",
    ],
)

magic_toolchains = use_extension(
    "//magic/extensions:toolchains.bzl",
    "magic_toolchains",
)
use_repo(magic_toolchains, "rules_magic_toolchains")

register_toolchains("@rules_magic_toolchains//...:all")

# Dev deps, kept to maintain parity with deps.bzl. Also, since Bazel depends on
# these, this helps minimize MODULE.bazel.lock bloat and churn.
bazel_dep(name = "rules_cc", version = "0.2.14", dev_dependency = True)
bazel_dep(name = "rules_python", version = "1.6.3", dev_dependency = True)

dev_toolchains = use_extension(
    "//magic/extensions:toolchains.bzl",
    "magic_toolchains",
    dev_dependency = True,
)
dev_toolchains.settings(
    enchanter = True,
    sorcerer = True,
    spellbreaker = True,
)
